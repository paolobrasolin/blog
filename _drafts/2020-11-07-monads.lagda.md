---
title: Three views of a monad
published: true
latex: katex
language: en
katex:
katex:
  macros:
    '\id': '\mathrm{id}'
antex:
  preamble:
    \usepackage{commutative-diagrams}
---


I once tried to ~~prove~~ convince myself of the equivalence of different monad implementations using Haskell and pseudocode.
It was unconvincing and extremely tedious, so I gave up pretty soon.
Now I have some AGDA under my belt and I can have some revenge.

Note that this article is literate AGDA, so you can load it and execute it.

<!--
TODO:
* use levels in definitions to account for more general sizes
* set precedence of _>>=_ and _>=>_ w/ respect to ∘ in order to remove a few parantheses
* define `a ● p ● z = ext (λ {x} → cong a (cong-app p (z x)))` to simplify proofs
-->

```agda
open import Function
open import Relation.Binary.PropositionalEquality
open import Relation.Binary.PropositionalEquality as Eq
open Eq.≡-Reasoning
```

## Mathematicians' monads

First things first, what's a monad for a mathematician?

A **monad** in a category $C$ is a functor $M\:C\to C$ together with two natural transformations, the unit $\eta\:\id_C\to M$ and the multiplication $\mu\:M^2\to M$, such that the diagrams expressing associativity and unit laws commute:

{% tex classes: [antex, display] %}
\begin{codi}
  \obj (S) { M^3 & M^2 \\ M^2 & M \\ };
  \mor M^3 M\mu:-> S-1-2 \mu:-> M;
  \mor[swap] * {\mu M}:-> S-2-1 \mu:-> *;
\end{codi}
\qquad
\begin{codi}
  \obj (S) { M & M^2 \\ M^2 & M \\ };
  \mor S-1-1 M\eta:-> S-1-2 \mu:-> S-2-2;
  \mor[swap] * {\eta M}:-> S-2-1 \mu:-> *;
  \mor:[double, double distance=.2em] * - *;
\end{codi}
{% endtex %}

This definition is exhaustive but a little terse.
Since we need to implement this, it will help to unpack it a little bit.

$M\:C\to C$ needs to be a functor, which means that:
* it maps each object $X$ in $C$ to an object $M(X)$ in $C$
* it maps each morphism $f\:X\to Y$ in $C$ to a morphism $M(f)\:M(X)\to M(Y)$ in $C$
* it preserves identities, i.e. $M(\id_X)=\id_{M(X)}$ for every object $X$ in $C$
* it preserves composition, i.e. $M(f\circ g)=M(f)\circ M(g)$ for every pair of composable morphisms $f$, $g$ in $C$

$\eta:\id_C\to M$ needs to be a natural transformation, which means that:
* it maps each object $X$ in $C$ to a morpism $\eta_X\:\id_C(X)\to M(X)$
* $\eta_Y\circ f=M(f)\circ\eta_X$ for every morphism $f\:X\to Y$ in $C$

$\mu:M^2\to M$ needs to be a natural transformation, which means that:
* it maps each object $X$ in $C$ to a morpism $\mu_X\:M(M(X))\to M(X)$
* $\mu_Y\circ M(M(f))=M(f)\circ\mu_X$ for every morphism $f\:X\to Y$ in $C$

The commutative diagrams expressing associativity and unit laws can be redrawn without the implicit notation typically used for natural transformations:

{% tex classes: [antex, display] %}
\begin{codi}[tetragonal=base 9em height 4em]
  \obj (S) { M(M(M(X))) & M(M(X)) \\ M(M(X)) & M(X) \\ };
  \mor S-1-1 M(\mu_X):-> S-1-2 \mu:-> S-2-2;
  \mor[swap] * {\mu_{M(X)}}:-> S-2-1 \mu_X:-> *;
\end{codi}
\qquad
\begin{codi}[tetragonal=base 7em height 4em]
  \obj (S) { M(X) & M(M(X)) \\ M(M(X)) & M(X) \\ };
  \mor S-1-1 M(\eta_X):-> S-1-2 \mu_X:-> S-2-2;
  \mor[swap] * {\eta_{M(X)}}:-> S-2-1 \mu_X:-> *;
  \mor:[double, double distance=.2em] * - *;
\end{codi}
{% endtex %}

for every object $X$ in $C$.
In equations,
* $\mu_X\circ M(\mu_X)=\mu_X\circ\mu_{M(X)}$ for every object $X$ in $C$
* $\mu_X\circ M(\eta_X)=\id_{M(X)}$ for every object $X$ in $C$
* $\mu_X\circ \eta_{M(X)}=\id_{M(X)}$ for every object $X$ in $C$

Everything is unpacked now and we just need to transcribe it into AGDA.
We'll call $\eta$, $\mu$ and the action of $M$ on morphisms respectively `unit`, `mult` and `fmap`.
Here's the result:

```agda
record MathMon (M : Set → Set) : Set₁ where
  field
    fmap : {A B : Set} → (A → B) → M A → M B
    unit : {A : Set} → A → M A
    mult : {A : Set} → M (M A) → M A
    -- functoriality
    fun-idty : ∀ {A : Set} → fmap {A} id ≡ id
    fun-comp : ∀ {A B C : Set} {f : B → C} {g : A → B} → fmap (f ∘ g) ≡ (fmap f) ∘ (fmap g)
    -- naturality
    nat-unit : ∀ {A B : Set} {f : A → B} → (fmap f) ∘ unit ≡ unit ∘ f
    nat-mult : ∀ {A B : Set} {f : A → B} → (fmap f) ∘ mult ≡ mult ∘ fmap (fmap f)
    -- monadicity
    unitˡ : ∀ {A : Set} → mult {A} ∘ unit ≡ id
    unitʳ : ∀ {A : Set} → mult {A} ∘ fmap unit ≡ id
    assoc : ∀ {A : Set} → mult {A} ∘ fmap mult ≡ mult ∘ mult
```

## Programmers' monads

Programmers use a terser definition of monad.
As an Haskell type class, a monad is simply

```haskell
class Monad m where
  return :: a -> m a
  (>>=) :: m a -> (a -> m b) -> m b -- pronounced "bind"
```

The programmer must ensure these pseudocode properties hold for any instance she implements:

```haskell
return x >>= f = f x                      -- left unit
m >>= return = m                          -- right unit
(m >>= f) >>= g = m >>= (\x -> f x >>= g) -- associativity
```

You can guess these are related to the monad laws we've seen before expressed in terms of `return` and `>>=`.

We have all we need to define an AGDA type for the programmers' definition of monads:

```agda
record ProgMon (M : Set → Set) : Set₁ where
  field
    return : {A : Set} → A -> M A
    _>>=_ : {A B : Set} → M A → (A → M B) -> M B
    -- monadicity
    unitˡ : ∀ {A B : Set} {x : A} {f : A → M B}
      → (return x) >>= f ≡ f x
    unitʳ : ∀ {A : Set} {m : M A}
      → m >>= return ≡ m
    assoc : ∀ {A B C : Set} {m : M A} {f : A → M B} {g : B → M C}
      → (m >>= f) >>= g ≡ m >>= λ{x → f x >>= g}
```

## Mathematicians VS programmers

We expect the two definitions to be equivalent.

To show we can build a `ProgMon` from a `MathMon` we need to know how `return` and `>>=` relate to `fmap`, `unit` and `mult`.

By either reading around[^progmon-ops-from-mathmon-ops] or trying to fill holes in AGDA you'll conclude that

[^progmon-ops-from-mathmon-ops]: TODO

```haskell
return x = unit x
x >>= f = mult (fmap f x)
```

We can now write a formal proof:

```agda
MathMon→ProgMon : {M : Set → Set} → MathMon M → ProgMon M
MathMon→ProgMon {M}
  record
    { fmap = fmap
    ; unit = unit
    ; mult = mult
    ; fun-comp = fun-comp
    ; fun-idty = _
    ; nat-unit = nat-unit
    ; nat-mult = nat-mult
    ; unitʳ = unitʳ
    ; unitˡ = unitˡ
    ; assoc = assoc
    }
  =
  record
    { return = unit
    ; _>>=_ = _>>=_
    ; unitˡ = λ {_} {_} {x} {f} →
      begin
        (unit x) >>= f          ≡⟨⟩
        mult (fmap f (unit x))  ≡⟨ cong mult (cong-app nat-unit x) ⟩
        mult (unit (f x))       ≡⟨⟩
        (mult ∘ unit) (f x)     ≡⟨ cong-app unitˡ (f x) ⟩
        id (f x)                ≡⟨⟩
        f x
      ∎
    ; unitʳ = λ {_} {m} →
      begin
        m >>= unit            ≡⟨⟩
        mult (fmap unit m)    ≡⟨⟩
        (mult ∘ fmap unit) m  ≡⟨ cong-app unitʳ m ⟩
        id m                  ≡⟨⟩
        m
      ∎
    ; assoc = λ {_} {_} {_} {m} {f} {g} →
      begin
        ((m >>= f) >>= g)                         ≡⟨⟩
        (mult (fmap f m)) >>= g                   ≡⟨⟩
        mult (fmap g (mult (fmap f m)))           ≡⟨⟩
        (mult ∘ fmap g ∘ mult ∘ fmap f) m         ≡⟨ cong mult (cong-app nat-mult (fmap f m)) ⟩
        (mult ∘ mult ∘ fmap (fmap g) ∘ fmap f) m  ≡⟨ cong (mult ∘ mult) (cong-app (sym fun-comp) m) ⟩
        (mult ∘ mult ∘ fmap (fmap g ∘ f)) m       ≡⟨ cong-app (cong (λ{h → h ∘ fmap (fmap g ∘ f)}) (sym assoc)) m ⟩
        (mult ∘ fmap mult ∘ fmap (fmap g ∘ f)) m  ≡⟨ cong mult (cong-app (sym fun-comp) m) ⟩
        (mult ∘ fmap (mult ∘ fmap g ∘ f)) m       ≡⟨⟩
        m >>= (mult ∘ fmap g ∘ f)                 ≡⟨⟩
        m >>= (λ{ x → (mult ∘ fmap g ∘ f) x })    ≡⟨⟩
        m >>= (λ{ x → f x >>= g })
      ∎
    }
    where
      _>>=_ : {A B : Set} → M A → (A → M B) -> M B
      _>>=_ x f = mult (fmap f x)
```


To show we can build a `MathMon` from a `ProgMon` we need to know how `fmap`, `unit` and `mult` relate to `return` and `>>=`.

By either reading around[^mathmon-ops-from-progmon-ops] or trying to fill holes in AGDA you'll conclude that

[^mathmon-ops-from-progmon-ops]: TODO

```haskell
fmap f x = x >>= (return . f)
mult x = x >>= id
```

We also need to postulate extensionality, but that's okay.

```agda
postulate
  ext : ∀ {A B : Set} {f g : A → B}
    → (∀ {x : A} → f x ≡ g x)
    → f ≡ g
```

Brace yourself:

```agda
ProgMon→MathMon : {M : Set → Set} → ProgMon M → MathMon M
ProgMon→MathMon {M}
  record
    { return = return
    ; _>>=_ = _>>=_
    ; unitˡ = unitˡ
    ; unitʳ = unitʳ
    ; assoc = assoc
    }
  =
  record
    { fmap = fmap
    ; unit = return
    ; mult = mult
    ; fun-comp = λ {_} {_} {_} {f} {g} →
      begin
        fmap (f ∘ g)                                           ≡⟨⟩
        (λ x → fmap (f ∘ g) x)                                 ≡⟨⟩
        (λ x → x >>= (return ∘ (f ∘ g)))                       ≡⟨⟩
        (λ x → x >>= (λ y → (return ∘ f ∘ g) y))               ≡⟨ ext (λ {x} → cong (_>>=_ x) (ext (sym unitˡ))) ⟩
        (λ x → x >>= (λ y → (return ∘ g) y >>= (return ∘ f)))  ≡⟨ ext (sym assoc) ⟩
        (λ x → ((x >>= (return ∘ g)) >>= (return ∘ f)))        ≡⟨⟩
        (λ x → (fmap f) (x >>= (return ∘ g)))                  ≡⟨⟩
        (λ x → (fmap f ∘ fmap g) x)                            ≡⟨⟩
        fmap f ∘ fmap g
      ∎
    ; fun-idty =
      begin
        fmap id                      ≡⟨⟩
        (λ x → (fmap id) x)          ≡⟨⟩
        (λ x → x >>= (return ∘ id))  ≡⟨⟩
        (λ x → x >>= return)         ≡⟨⟩
        (λ x → (id x) >>= return)    ≡⟨ ext unitʳ ⟩
        (λ x → id x)                 ≡⟨⟩
        id
      ∎
    ; nat-unit = λ {_} {_} {f} →
      begin
        fmap f ∘ unit                      ≡⟨⟩
        (λ x → (fmap f ∘ return) x)        ≡⟨⟩
        (λ x → return x >>= (return ∘ f))  ≡⟨ ext unitˡ ⟩
        (λ x → (return ∘ f) x)             ≡⟨⟩
        unit ∘ f
      ∎
    ; nat-mult = λ {_} {_} {f} →
      begin
        fmap f ∘ mult                                         ≡⟨⟩
        (λ x → (fmap f ∘ mult) x)                             ≡⟨⟩
        (λ x → (mult x) >>= (return ∘ f))                     ≡⟨⟩
        (λ x → (x >>= id) >>= (return ∘ f))                   ≡⟨ ext assoc ⟩
        (λ x → x >>= (λ y → id y >>= (return ∘ f)))           ≡⟨⟩
        (λ x → x >>= (λ y → y >>= (return ∘ f)))              ≡⟨⟩
        (λ x → x >>= (λ y → fmap f y))                        ≡⟨⟩
        (λ x → x >>= (λ y → (id ∘ (fmap f)) y))               ≡⟨ ext (λ {x} → cong (_>>=_ x) (ext (sym unitˡ))) ⟩
        (λ x → x >>= (λ y → ((return ∘ (fmap f)) y) >>= id))  ≡⟨ ext (sym assoc) ⟩
        (λ x → (x >>= (return ∘ (fmap f))) >>= id)            ≡⟨⟩
        (λ x → (fmap (fmap f) x) >>= id)                      ≡⟨⟩
        (λ x → (mult ∘ fmap (fmap f)) x)                      ≡⟨⟩
        mult ∘ fmap (fmap f)
      ∎
    ; unitʳ =
      begin
        mult ∘ fmap unit                                ≡⟨⟩
        (λ x → (mult ∘ fmap return) x)                  ≡⟨⟩
        (λ x → fmap return x >>= id)                    ≡⟨⟩
        (λ x → (x >>= (return ∘ return)) >>= id)        ≡⟨ ext assoc ⟩
        (λ x → x >>= (λ y → return (return y) >>= id))  ≡⟨ ext (λ {x} → cong (_>>=_ x) (ext unitˡ)) ⟩
        (λ x → x >>= (λ y → return y))                  ≡⟨⟩
        (λ x → x >>= return)                            ≡⟨ ext unitʳ ⟩
        (λ x → id x)                                    ≡⟨⟩
        id
      ∎
    ; unitˡ =
      begin
        mult ∘ unit                ≡⟨⟩
        (λ x → (mult ∘ return) x)  ≡⟨⟩
        (λ x → return x >>= id)    ≡⟨ ext unitˡ ⟩
        (λ x → id x)               ≡⟨⟩
        id
      ∎
    ; assoc =
      begin
        mult ∘ fmap mult                                ≡⟨⟩
        (λ x → (mult ∘ fmap mult) x)                    ≡⟨⟩
        (λ x → (fmap mult x) >>= id)                    ≡⟨⟩
        (λ x → (x >>= (return ∘ mult)) >>= id)          ≡⟨ ext assoc ⟩
        (λ x → x >>= (λ y → (return ∘ mult) y >>= id))  ≡⟨ ext (λ {x} → cong (_>>=_ x) (ext unitˡ)) ⟩
        (λ x → x >>= (λ y → mult y))                    ≡⟨⟩
        (λ x → x >>= (λ y → y >>= id))                  ≡⟨ ext (sym assoc) ⟩
        (λ x → (x >>= id) >>= id)                       ≡⟨⟩
        (λ x → mult (x >>= id))                         ≡⟨⟩
        (λ x → (mult ∘ mult) x)                         ≡⟨⟩
        mult ∘ mult
      ∎
    }
  where
    fmap : {A B : Set} → (A → B) → M A → M B
    fmap f x = x >>= (return ∘ f)
    unit : {A : Set} → A → M A
    unit x = return x
    mult : {A : Set} → M (M A) → M A
    mult x = x >>= id
```

Bang! `MathMon` is equivalent to `ProgMon` and we can sleep peacefully.


## Aesthetes' monads

We're not done yet: a third common characterization of a monad exists!

Programmers define the `>=>` operator (pronounced _fish_) as

```haskell
(>=>) :: Monad m => (a -> m b) -> (b -> m c) -> (a -> m c)
(m >=> n) x  = m x >>= n
```

The type signature is somewhat more intuitive than `>>=`.
We can build a new monad type class replacing by `>>=` in the old one:

```haskell
class Monad m where
  return :: a -> m a
  (>=>) :: (a -> m b) -> (b -> m c) -> a -> m c
```

The monad laws can be rewritten in term of `>=>`:

```haskell
return >=> g = g                   -- left unit
f >=> return = f                   -- right unit
(f >=> g) >=> h = f >=> (g >=> h)  -- associativity
```

Their meaning is finally apparent!

Convinced of having struck gold we can define an AGDA type:

```agda
record DopeMon (M : Set → Set) : Set₁ where
  field
    return : {A : Set} → A -> M A
    _>=>_ : {A B C : Set} → (A → M B) → (B → M C) → (A → M C)
    -- monadicity
    unitˡ : ∀ {A C : Set} {g : A → M C}
      → return >=> g ≡ g
    unitʳ : ∀ {A B : Set} {f : A → M B}
      → f >=> return ≡ f
    assoc : ∀ {A B C D : Set} {f : A → M B} {g : B → M C} {h : C → M D}
      → (f >=> g) >=> h ≡ f >=> (g >=> h)
```

Building a `DopeMon` from a `MathMon` or a `ProgMon` poses no problem.

Building a `MathMon` or a `ProgMon` from a `DopeMon` we hit a hard wall:
when proving properties we're left with two kind of holes which cannot be filled.

This means that _this characterization is weaker than the previous two_ and we've realized that by rediscovering the missing axioms.



`>=>` is also called Kleisli composition and this is a clue to find its complete mathematical definition: it is the composition of a Kleisli category, which is a category naturally associated with any monad.



```agda
record KlslMon (M : Set → Set) : Set₁ where
  field
    unit : {A : Set} → A -> M A
    _⋆ : {A B : Set} → (A → M B) → (M A → M B)
    -- extension axioms
    ext-unitˡ : ∀ {A : Set}
      → (unit {A} ⋆) ≡ id
    ext-unitʳ : ∀ {A B : Set} {f : A → M B}
      → (f ⋆) ∘ unit ≡ f
    ext-assoc : ∀ {A B C : Set} {f : A → M B} {g : B → M C}
      → ((g ⋆) ∘ f)⋆ ≡ (g ⋆) ∘ (f ⋆)
  ---
  _>=>_ : {A B C : Set} → (A → M B) → (B → M C) → (A → M C)
  _>=>_ f g = (g ⋆) ∘ f
  -- monadicity
  unitˡ : ∀ {A C : Set} {g : A → M C}
    → unit >=> g ≡ g
  unitˡ {_} {_} {g} =
    begin
      unit >=> g    ≡⟨⟩
      (g ⋆) ∘ unit  ≡⟨ ext-unitʳ ⟩
      g
    ∎
  unitʳ : ∀ {A B : Set} {f : A → M B}
    → f >=> unit ≡ f
  unitʳ {_} {_} {f} =
    begin
      f >=> unit    ≡⟨⟩
      (unit ⋆) ∘ f  ≡⟨ cong (_∘ f) ext-unitˡ ⟩
      f
    ∎
  assoc : ∀ {A B C D : Set} {f : A → M B} {g : B → M C} {h : C → M D}
    → (f >=> g) >=> h ≡ f >=> (g >=> h)
  assoc {_} {_} {_} {_} {f} {g} {h} =
    begin
      (f >=> g) >=> h      ≡⟨⟩
      (h ⋆) ∘ ((g ⋆) ∘ f)  ≡⟨⟩
      ((h ⋆) ∘ (g ⋆)) ∘ f  ≡⟨ cong (_∘ f) (sym ext-assoc) ⟩
      (((h ⋆) ∘ g)⋆) ∘ f   ≡⟨⟩
      f >=> (g >=> h)
    ∎
```

## Aesthetes VS mathematicians

```agda
MathMon→KlslMon : {M : Set → Set} → MathMon M → KlslMon M
MathMon→KlslMon {M}
  record
    { fmap = fmap
    ; unit = unit
    ; mult = mult
    ; fun-comp = fun-comp
    ; fun-idty = _
    ; nat-unit = nat-unit
    ; nat-mult = nat-mult
    ; unitʳ = unitʳ
    ; unitˡ = unitˡ
    ; assoc = assoc
    }
  =
  record
    { unit = unit
    ; _⋆ = _⋆
    ; ext-unitˡ = λ {_} →
      begin
        (unit ⋆)          ≡⟨⟩
        mult ∘ fmap unit  ≡⟨ unitʳ ⟩
        id
      ∎
    ; ext-unitʳ = λ {_} {_} {f} →
      begin
        (f ⋆) ∘ unit          ≡⟨⟩
        mult ∘ fmap f ∘ unit  ≡⟨ cong (mult ∘_) nat-unit ⟩
        mult ∘ unit ∘ f       ≡⟨ cong (_∘ f) unitˡ ⟩
        f
      ∎
    ; ext-assoc = λ {_} {_} {_} {f} {g} →
      begin
        (((g ⋆) ∘ f) ⋆)                            ≡⟨⟩
        mult ∘ fmap (mult ∘ fmap g ∘ f)            ≡⟨ cong (mult ∘_) fun-comp ⟩
        mult ∘ fmap mult ∘ fmap (fmap g ∘ f)       ≡⟨ cong ((mult ∘ fmap mult) ∘_) fun-comp ⟩
        mult ∘ fmap mult ∘ fmap (fmap g) ∘ fmap f  ≡⟨ cong (_∘ (fmap (fmap g) ∘ fmap f)) assoc ⟩
        mult ∘ mult ∘ fmap (fmap g) ∘ fmap f       ≡⟨ cong (λ h → (mult ∘ h ∘ fmap f)) (sym nat-mult) ⟩
        mult ∘ fmap g ∘ mult ∘ fmap f              ≡⟨⟩
        (g ⋆) ∘ (f ⋆)
      ∎

    }
  where
    _⋆ : {A B : Set} → (A → M B) → (M A → M B)
    _⋆ f = mult ∘ fmap f
```

```agda
KlslMon→MathMon : {M : Set → Set} → KlslMon M → MathMon M
KlslMon→MathMon {M}
  record
    { unit = unit
    ; _⋆ = _⋆
    ; ext-unitˡ = ext-unitˡ
    ; ext-unitʳ = ext-unitʳ
    ; ext-assoc = ext-assoc
    }
  =
  record
    { fmap = fmap
    ; unit = unit
    ; mult = mult
    ; fun-comp = λ {_} {_} {_} {f} {g} →
      begin
        fmap (f ∘ g)                ≡⟨⟩
        (unit ∘ f ∘ g)⋆             ≡⟨ cong (λ h → (h ∘ g)⋆) (sym ext-unitʳ) ⟩
        ((unit ∘ f)⋆ ∘ unit ∘ g)⋆   ≡⟨ ext-assoc ⟩
        (unit ∘ f)⋆ ∘ (unit ∘ g)⋆   ≡⟨⟩
        fmap f ∘ fmap g
      ∎
    ; fun-idty =
      begin
        fmap id       ≡⟨⟩
        (unit ∘ id)⋆  ≡⟨⟩
        unit ⋆        ≡⟨ ext-unitˡ ⟩
        id
      ∎
    ; nat-unit = λ {_} {_} {f} →
      begin
        fmap f ∘ unit       ≡⟨⟩
        (unit ∘ f)⋆ ∘ unit  ≡⟨ ext-unitʳ ⟩
        unit ∘ f
      ∎
    ; nat-mult = λ {_} {_} {f} →
      begin
        fmap f ∘ mult                 ≡⟨⟩
        (unit ∘ f)⋆ ∘ id ⋆            ≡⟨ sym ext-assoc ⟩
        ((unit ∘ f)⋆ ∘ id )⋆          ≡⟨⟩
        (id ∘ (unit ∘ f)⋆)⋆           ≡⟨ cong (λ h → (h ∘ (unit ∘ f)⋆)⋆) (sym ext-unitʳ) ⟩
        (id ⋆ ∘ unit ∘ (unit ∘ f)⋆)⋆  ≡⟨ ext-assoc ⟩
        id ⋆ ∘ (unit ∘ (unit ∘ f)⋆)⋆  ≡⟨⟩
        mult ∘ fmap (fmap f)
      ∎
    ; unitʳ =
      begin
        mult ∘ fmap unit       ≡⟨⟩
        id ⋆ ∘ (unit ∘ unit)⋆  ≡⟨ sym ext-assoc ⟩
        (id ⋆ ∘ unit ∘ unit)⋆  ≡⟨ cong (λ h → (h ∘ unit)⋆) ext-unitʳ ⟩
        (id ∘ unit)⋆           ≡⟨⟩
        unit ⋆                 ≡⟨ ext-unitˡ ⟩
        id
      ∎
    ; unitˡ =
      begin
        mult ∘ unit                ≡⟨⟩
        id ⋆ ∘ unit                ≡⟨ ext-unitʳ ⟩
        id
      ∎
    ; assoc =
      begin
        mult ∘ fmap mult             ≡⟨⟩
        id ⋆ ∘ (unit ∘ id ⋆)⋆        ≡⟨ sym ext-assoc ⟩
        (id ⋆ ∘ unit ∘ id ⋆)⋆        ≡⟨ cong (λ h → (h ∘ id ⋆)⋆) ext-unitʳ ⟩
        (id ∘ id ⋆)⋆                 ≡⟨⟩
        (id ⋆ ∘ id)⋆                 ≡⟨ ext-assoc ⟩
        id ⋆ ∘ id ⋆                  ≡⟨⟩
        mult ∘ mult
      ∎
    }
  where
    -- NOTE: easily guessed by hole filling
    fmap : {A B : Set} → (A → B) → M A → M B
    fmap f = (unit ∘ f) ⋆
    mult : {A : Set} → M (M A) → M A
    mult = id ⋆
```

## Aesthetes VS programmers

```agda
ProgMon→KlslMon : {M : Set → Set} → ProgMon M → KlslMon M
ProgMon→KlslMon {M}
  record
    { return = return
    ; _>>=_ = _>>=_
    ; unitˡ = unitˡ
    ; unitʳ = unitʳ
    ; assoc = assoc
    }
  =
  record
    { unit = unit
    ; _⋆ = _⋆
    ; ext-unitˡ = λ {_} →
      begin
        (unit ⋆) ≡⟨ {!!} ⟩
        id
      ∎
    ; ext-unitʳ = λ {_} {_} {f} →
      begin
        (f ⋆) ∘ unit ≡⟨ {!!} ⟩
        f
      ∎
    ; ext-assoc = λ {_} {_} {_} {f} {g} →
      begin
        (((g ⋆) ∘ f) ⋆) ≡⟨ {!!} ⟩
        (g ⋆) ∘ (f ⋆)
      ∎
    }
  where
    unit : {A : Set} → A -> M A
    unit = return
    _⋆ : {A B : Set} → (A → M B) → (M A → M B)
    _⋆ = {!!}
```

```agda
KlslMon→ProgMon : {M : Set → Set} → KlslMon M → ProgMon M
KlslMon→ProgMon {M}
  record
    { unit = unit
    ; _⋆ = _⋆
    ; ext-unitˡ = ext-unitˡ
    ; ext-unitʳ = ext-unitʳ
    ; ext-assoc = ext-assoc
    }
  =
  record
    { return = return
    ; _>>=_ = _>>=_
    ; unitˡ = λ {_} {_} {x} {f} →
      begin
        (unit x >>= f) ≡⟨ {!!} ⟩
        f x
      ∎
    ; unitʳ = λ {_} {m} →
      begin
        (m >>= unit) ≡⟨ {!!} ⟩
        m
      ∎
    ; assoc = λ {_} {_} {_} {m} {f} {g} →
      begin
        ((m >>= f) >>= g) ≡⟨ {!!} ⟩
        (m >>= (λ { x → f x >>= g }))
      ∎
    }
  where
    return : {A : Set} → A -> M A
    return = unit
    _>>=_ : {A B : Set} → M A → (A → M B) -> M B
    _>>=_ = {!!}
```



-----------------
```text
ProgMon→FunkMon : {M : Set → Set} → ProgMon M → FunkMon M
_>=>_ : {A B C : Set} → (A → M B) → (B → M C) → A → M C
_>=>_ m n x = m x >>= n

FunkMon→ProgMon : {M : Set → Set} → FunkMon M → ProgMon M
_>>=_ : {A B : Set} → M A → (A → M B) → M B
_>>=_ ma f = (id >=> f) ma
-- _>>=_ ma f = ((λ _ → ma) >=> f) () -- NOTE: usually one sees this in Haskell


MathMon→FunkMon : {M : Set → Set} → MathMon M → FunkMon M
_>=>_ : {A B C : Set} → (A → M B) → (B → M C) → A → M C
_>=>_ f g x = mult (fmap g (f x))

FunkMon→MathMon : {M : Set → Set} → FunkMon M → MathMon M
fmap : {A B : Set} → (A → B) → M A → M B
fmap f x = (id >=> (unit ∘ f)) x
mult : {A : Set} → M (M A) → M A
mult x = (id >=> id) x
-- NOTE: these are the usual definitions but they have an ambiguous domain
-- fmap f x = ((const x) >=> (unit ∘ f)) _
-- mult x = ((const x) >=> id) _
postulate
-- TODO: I'm both unable to prove this and unable to find another way
magic : {A B C : Set} → {f : A → M B} → {g : B → M C}
→ (f >=> g) ≡ (id >=> g) ∘ f
boyah : {A B C : Set} → {f : A → B} → {g : B → C}
→ (unit ∘ f) >=> (unit ∘ g) ≡ unit ∘ (g ∘ f)
```
